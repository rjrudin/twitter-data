plugins {
    id "com.marklogic.ml-gradle" version "2.7.0"
    id "com.github.jk1.dependency-license-report" version "0.3.11"
}

repositories {
    mavenCentral()
    maven {url "http://developer.marklogic.com/maven2/"}
    maven {url "http://rjrudin.github.io/marklogic-java/releases"}
    maven {url "http://repository.cloudera.com/artifactory/cloudera-repos/" }
}

configurations {
    mlcp
}

dependencies {
    mlcp "com.marklogic:mlcp-Hadoop2:1.3-2"
    mlcp "com.marklogic:marklogic-mapreduce2:2.1.3"
}

task downloadTweets {
    doLast {
        new File("build").mkdir()
        def f = new File('build/tweets.zip')
        if (f == null || !f.exists()) {
            new URL(tweetsZipUrl).withInputStream{ i -> f.withOutputStream{ it << i }}
        }
    }
}

task importTweets(type: com.marklogic.gradle.task.MlcpTask) {
    dependsOn = ["downloadTweets", "mlLoadModules"]
    classpath = configurations.mlcp
    command = "IMPORT"
    port = Integer.parseInt(mlRestPort)
    database = mlAppConfig.contentDatabaseName
    input_file_path = "build/tweets.zip"
    input_compressed = "true"
    output_collections = "tweets"
    output_permissions = "rest-reader,read,rest-writer,update"
    output_uri_replace = ".*tweets.zip,''"
    thread_count = 16
    transform_module = "/ext/mlcp/tweet-transform.xqy"
    transform_namespace = "urn:sample:transform"
}
